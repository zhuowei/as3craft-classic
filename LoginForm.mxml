<?xml version="1.0" encoding="utf-8"?>
<s:Panel xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" title="Login">
<fx:Script>
	<![CDATA[
		import flash.events.*;
		import flash.media.*;
		import flash.net.*;
		import flash.geom.*;
		import mx.controls.Alert;
		public var host:String;
		public var port:int;
		public var username:String;
		public var key:String;
		private var mcWebView:StageWebView;
		public static const WEBVIEW_HEIGHT:Number=400;
		//public static const WEBVIEW_WIDTH:Number=480;
		public static const JAVASCRIPT_INJECT_URL:String='javascript:var mcurlnav=function(){var username=document.getElementsByName'+
			'("username")[0].value;var server=document.getElementsByName("server")[0].value;'+
			'var port=document.getElementsByName("port")[0].value;var mppass=document.getElementsByName("mppass")[0].value;'+
			'window.location="mc://"+server+":"+port+"/"+username+"/"+mppass;};mcurlnav();';
		/** WARNING: STUPID KLUTGE! 
		* Show a Webview to minecraft.net.
		*/
		public function mineLogin():void{
			mcLoginButton.visible=false;
			mcLoginWebviewSpacer.width=this.width *0.9;
			mcLoginWebviewSpacer.height=WEBVIEW_HEIGHT;
			mcWebView=new StageWebView();
			mcWebView.addEventListener("locationChanging", function(e:LocationChangeEvent):void{
					trace(e.location.substr(0,5), e.location.substr(0,11));
					if(e.location.substr(0,5)=="mc://"){
						trace("got mc url: " + e.location);
						e.preventDefault();
						closeMCWebpage();
						mcURLLogin(e.location);
						return;
					}
					if(e.location.substr(0,11)=="javascript:"){
						return;
					}
					if(e.location.indexOf("minecraft.net")<0){
						e.preventDefault();
						Alert.show("Please do not navigate outside of Minecraft.net. You'll hurt my feelings.");
						return;
					}

				});
			mcWebView.addEventListener("locationChange", function(e:LocationChangeEvent):void{
					if(e.location.substr(0,5)=="mc://"){
						trace("got mc url: " + e.location);
						closeMCWebpage();
						mcURLLogin(e.location);
						return;
					}
					if(e.location.indexOf("minecraft.net/classic/play")!=-1){
						trace("found server login");
						setTimeout(function():void{mcWebpageLoad();}, 500); //let it load for a bit
					}
			});
			mcWebView.addEventListener("complete", function(e:Event):void{
					if(mcWebView.location.substr(0,5)=="mc://"){
						trace("got mc url: " + mcWebView.location);
						var loc:String = mcWebView.location;
						closeMCWebpage();
						mcURLLogin(loc);
						return;
					}
			});
			mcWebView.stage=this.stage;
			var webviewPoint:Point=mcLoginWebviewSpacer.contentToGlobal(new Point(0,0));
			mcWebView.viewPort=new Rectangle(webviewPoint.x, webviewPoint.y, mcLoginWebviewSpacer.width, WEBVIEW_HEIGHT);
			mcWebView.loadURL("https://www.minecraft.net/login");
		}
		/** injects the mc:// url maker into the StageWebView. */
		protected function mcWebpageLoad():void{
			mcWebView.loadURL(JAVASCRIPT_INJECT_URL);
		}
		protected function mcURLLogin(url:String):void{
			if(mcWebView!=null){
				closeMCWebpage();
			}
			var urlobj:MCURL;
			try{
				urlobj=MCURL.decode(url);
				dispatchLogin(urlobj.host, urlobj.port, urlobj.username, urlobj.key);
			}
			catch(e:URIError){
				Alert.show("The URL you have entered is not a valid Minecraft Direct Connect URL.");
			}
			catch(e:Error){
				Alert.show("The URL you have entered cannot be parsed. \n" + e.toString());
			}
		}
		private function dispatchLogin(host:String, port:int, username:String, key:String):void{
			this.host=host;
			this.port=port;
			this.username=username;
			this.key=key;
			dispatchEvent(new Event("login"));
		}
		private function closeMCWebpage():void{
			mcWebView.stage=null;
			mcWebView.dispose();
			mcWebView=null;
			mcLoginWebviewSpacer.width=0;
			mcLoginWebviewSpacer.height=0;
			mcLoginButton.visible=true;
		}
		private function findOnPage(str:String):void{
			if(mcWebView){
				mcWebView.loadURL("javascript:window.find('" + str + "');window.scrollTo(0, window.getSelection().focusNode.parentNode.offsetTop)");
			}
		}
]]>
</fx:Script>
<s:layout>
	<s:VerticalLayout/>
</s:layout>
<s:VGroup id="loginPane">

<s:VGroup visible="{!mcLoginButton.visible}">
	<s:Label id="mcLoginLabel" text="Please log in to the Minecraft website and choose a Minecraft Classic server. " fontSize="14"/>
	<s:Label text="After logging in, you can paste a play.jsp or classic/play url in the box below." fontSize="12"/>
	<s:HGroup><s:TextInput id="playUrlInput"/><s:Button label="Paste" click="playUrlInput.text=Clipboard.generalClipboard.getData(ClipboardFormats.TEXT_FORMAT).toString();"/>
	<s:Button label="Go" click="mcWebView.loadURL(playUrlInput.text)"/></s:HGroup>
	
	<!--<s:TextInput id="findInput"/><s:Button label="Find" click="findOnPage(findInput.text)" needsSoftKeyboard="true" visible="False"/>-->
</s:VGroup>
<mx:Spacer id="mcLoginWebviewSpacer" width="0" height="0"/>
<s:Button id="mcLoginButton" click="mineLogin()" label="Login to Minecraft.net"/>
</s:VGroup>
<s:Label text="Or enter a Minecraft Direct Connect url: "/>
<s:HGroup>
	<s:TextInput id="mcURLInput" width="{this.width*0.75}" enter="mcURLLogin(mcURLInput.text)" needsSoftKeyboard="true"/>
	<s:Button label="Paste" click="mcURLInput.text=Clipboard.generalClipboard.getData(ClipboardFormats.TEXT_FORMAT).toString();"/>
	<s:Button label="Go!" click="mcURLLogin(mcURLInput.text)"/>
</s:HGroup>
</s:Panel>
